= Funcions and Functional Programming
:source-language: clojure
:source-highlighter: coderay
:sectnums:
:imagesdir: ../img
:linkcss:
:stylesdir: ../
:stylesheet: my-asciidoctor.css
:docinfo1:
:toc: right


== 함수형 언어란 무엇인가?

다음의 조건 네 가지를 충족시키면, 그 함수는 일급 함수(fisrt-class function)라고 부르고,
일급 함수를 지원하는 언어는 '전통적인 의미'에서의 함수형 언어라고 부를 수 있다.

. 함수 객체를 변수에 대입할 수 있다.
+
[source]
....
(def double (fn [a] (* 2 a)))   ; <1>

(double 10)    ; => 20

(def double2 double)

(double2 10)   ; => 20
....
<1> 특수 함수(special form) ``fn``을 이용해 먼저 함수 객체를 생성한 후, 이 객체를
    ``double``이라는 심볼에 바인딩한다.

. 함수 객체를 함수의 인수로 전달할 수 있다.
+
[source]
....
(defn calc [op a b]
  (op a b))

(calc + 10 20)   ; => 30   <1>
(calc * 10 20)   ; => 200
....
<1> 함수 객체 ``+``를 `calc` 함수의 첫 번째 인수로 넘긴다.

. 함수 객체를 함수의 반환값으로 반환할 수 있다.
+
[source]
....
(defn operator [op]
  (cond
    (= op :plus)  +     ; <1>
    (= op :minus) -))   ; <1>

((operator :plus) 10 20)    ; => 30
((operator :minus) 10 20)   ; => -10
....
<1> 함수 객체 ``+`` 또는 ``-``를 함수의 반환값으로 반환한다.

. 함수 객체를 동적으로 생성할 수 있다.
+
[source]
....
(defn multiple [n]
  (fn [a]   ; <1>
    (* n a)))

(def double (multiple 2))
; => #<Var@4340684b: #object[user$multiple$fn__12286 0x26a894a2]>

(def triple (multiple 3))
; => #<Var@fc4d31e: #object[user$multiple$fn__12286 0x1a4b7c5d]>

(double 10)   ; => 20
(triple 10)   ; => 30
....
<1> 함수를 동적으로 생성해 반환한다. 특수 함수 ``fn``이 수행될 때마다 새로운 함수 객체가
    생성된다.

TIP: 특히 위의 두 번째와 세 번째 조건을 충족시키는 함수를 고차 함수(higher-order
     function)라고 부른다.

오늘날 대부분의 언어가 함수형 언어의 영향을 받아, 위의 네 가지 조건을 모두 지원하거나
일부를 지원하고 있다. 그래서 루비나 파이썬, 심지어는 자바스크립트 언어마저도 위의 조건들을
모두 충족하고 있다. 그렇다면 이 세상은 이미 함수형 언어가 지배하고 있는 것인가? 함수형
언어를 어떻게 정의하느냐에 따라 의견이 달라질 수 있겠지만, 클로저 언어의 입장에서는 그렇게
보지 않는다. '현대적인 의미'에서, 즉 진정한 의미에서의 함수형 언어는 위의 네 가지 조건
이외에 한 가지 조건을 더 충족시켜야 하는데, 그것은 바로 클로저나 해스켈처럼
불변형(immutable) 자료 구조를 기본적으로 지원해야 한다는 것이다.


== 함수 정의하기

이제 함수를 직접 정의해 보자.


indexterm:[defn]

=== defn

클로저에서 함수를 정의하는 가장 일반적인 방법은 ``defn``을 이용하는 것이다.

.defn
[listing]
----
(defn name doc-string? metadata-map? [params*] prepost-map? body)
(defn name doc-string? metadata-map? ([params*] prepost-map? body)+)

prepost-map := {:pre  [pre-expr+]
                :post [post-expr+]}
----

좀 복잡하게 보인다. 그래서 가장 간단한 예부터 차근차근 설명해 보겠다.

[source]
....
(defn foo                   ; <1>
  "세 개의 숫자를 곱한다."   ; <2>
  [a b c]                   ; <3>
  (* a b c))                ; <4>

(foo 2 3 4)   ;=> 24
....
<1> 함수의 이름(name)이다.
<2> 함수를 설명하는 doc-string이다.
<3> 함수의 인수들(params)이다.
<4> 함수의 몸체(body)이다.

같은 이름을 사용하지만, 인수의 개수를 달리한 함수 본체를 여러 개 정의할 수도 있다. 이때
정의된 함수들 중에서 어떤 함수 본체가 호출될지는 오직 인수의 개수에 의해서만 결정이 되고,
타입은 전혀 고려의 대상이 아니다.

[source]
....
(defn foo
  "숫자를 곱한다."
  ([a b]                                   ; <1>
   (println "Called with 2 arguments.")
   (* a b))
  ([a b c]                                 ; <2>
   (println "Called with 3 arguments.")
   (* a b c)))

(foo 2 3)
;>> Called with 2 arguments.
;=> 6

(foo 2 3 4)
;>> Called with 3 arguments.
;=> 24
....
<1> 두 개의 인수가 주어질 때 호출되는 함수를 정의한다.
<2> 세 개의 인수가 주어질 때 호출되는 함수를 정의한다.


==== metadata-map을 사용한 경우

[source]
....
(defn greet
  "인사말을 전한다."                 ; <1>
  {:tag String :other-meta-data 5}   ; <2>
  [name]
  (format "Hello, %s" name))

(meta #'greet)
;=> {:arglists ([name]), :tag java.lang.String, :other-meta-data 5, :line 10,
;    :column 6, :file "*cider-repl localhost:53283*", :name greet, :ns #namespace[user]}
....

<1> 함수의 doc-string이다.
<2> 함수의 ``metadata-map``이다.

위의 코드는 아래의 코드와 동일한 결과를 낳는다.

[source]
....
(defn ^{:tag String :other-meta-data 5}   ; <1>
  greet
  [name]
  (format "Hello, %s" name))

(meta #'greet)
;=> {:tag java.lang.String, :other-meta-data 5, :arglists ([name]), :line 15,
;    :column 6, :file "*cider-repl localhost:53283*", :name greet2, :ns #namespace[user]}
....
<1> var인 greet에 대한 metadata map이다.

결과적으로 var인 ``greet``에 대한 metadata를 표기하는 방식을 두 가지 제공하고 있는
셈이다. 그런데 실제로는 metadata를 표시할 때, 첫 번쨰 방식보다는 두 번쨰째 방식이 더 많이
사용된다. 하지만, 표시해야 하는 metadata의 내용이 아주 많은 경우에는 첫 번째 방식이 두
번째 방식보다 코드가 더 깔끔하게 보이는 장점은 있다.


==== prepost-map을 사용한 경우

`:pre` 뒤에는 함수를 실행하기 전에 그 인수를 검사(validation)하는 식들이 벡터 기호 안에
나열될 수 있다. `:post` 뒤에는 함수가 반환한 값(이 값은 ``%``로 표기한다)을 검사하는
식들이 벡터 기호 안에 나열될 수 있다. ``:pre``와 ``:post`` 둘 중에 한 개만 올 수도
있다. 이 검사는 ``\*assert*``의 값이 ``true``일 때만 실행되고, ``false``일 때는 실행되지
않는다. 게다가 이 검사는 내부적으로 `assert` 매크로를 이용해 구현되어 있어서,
``\*assert*``의 값이 ``false``일 때는 아무것으로도 확장되지 않아, 실행시 아무런 부하도
일으키지 않는다.

[source]
....
*assert*   ;=> true

(defn constrained-fn [f x]
  {:pre  [(pos? x) (integer? x)]
   :post [(= % (* 2 x)) (integer? %)]}
  (f x))

(constrained-fn #(* 2 %) 2)
;=> 4

(constrained-fn #(* 2 %) -2)
;>> AssertionError Assert failed: (pos? x)

(constrained-fn #(* 2 %) 2.0)
;>> AssertionError Assert failed: (integer? x)

(constrained-fn #(* 3 %) 2)
;>> AssertionError Assert failed: (= % (* 2 x))

;; *assert*를 false로 설정한다.
(set! *assert* false)
*assert*   ;=> false

;; constrained-fn을 재컴파일해 주어야 한다. 검사할 때 내부적으로 이용되는 assert는
;; 매크로이고, 매크로 확장은 컴파일 이전에 이루어지기 때문이다.
(defn constrained-fn [f x]
  {:pre  [(pos? x) (integer? x)]
   :post [(= % (* 2 x)) (integer? %)]}
  (f x))

;; 이번에는 검사를 하지 않아 예외가 발생하지 않았다.
(constrained-fn #(* 2 %) -2)
;=> -4
....


==== defn-

``defn-``로 함수를 정의하면, 이 함수는 해당 이름공간 내에서만 호출할 수 있는 함수가 된다.

[source]
....
user>(defn- private-add [a b]
        (+ a b))
#'user/private-add

user>  (private-add 5 6)
11

user> (ns my-new-namespace)
nil

my-new-namespace> (user/private-add 7 8)     ; <1>
CompilerException java.lang.IllegalStateException: var: #'user/private-add is not public

my-new-namespace> (#'user/private-add 7 8)   ; <2>
15
....
<1> `user/private-add` 함수는 `user` 이름공간 밖에서는 호출할 수 없다.

<2> 하지만 `user/private-add`의 varfootnote:[``#'``는 var의 reader 매크로이다.]를 통해서는
    호출할 수 있다. 이것은 private 함수도 Unit Test를 실행할 때는 외부 이름공간에서
    테스트될 필요가 있기 때문에 허용하고 있다.

그리고 다음의 두 코드는 완전히 같은 일을 수행한다. 다시 말해, ``(defn- ...)``는 ``(defn
^:private ...)``의 구문 단축형(syntactic sugar)이다.

[source]
....
(defn- private-add [a b]
  (+ a b))

(defn ^:private private-add
  [a b]
  (+ a b))
....


=== fn

indexterm:[fn]

.fn 특수 형식
[listing]
----
(fn name? [params*] prepost-map? exprs*)
(fn name? ([params*] prepost-map? exprs*)+)
----

``fn``은 무명(anonymous)footnote:[일반적으로 anonymous function을 흔히들 익명 함수라고
번역하고 있는데, '익명'은 이미 있는 이름을 '숨긴다'는 의미이기 때문에, '무명'으로 번역하는
것이 옳다고 본다.]의 함수 객체를 생성하는 클로저의 특수 형식(special forms)이다.

``defn``으로 함수를 다음과 같이 정의하면

[source]
....
(defn add [a b] (+ a b))
....

실제로는 다음과 같은 방식footnote:[실제로는 훨씬 복잡하게 정의되어 있지만, 이곳에서는
최대한으로 단순화했다.]으로 두 단계로 나뉘어 처리된다.

[source]
....
(def add                   ; <2>
  (fn [a b] (+ a b)))      ; <1>

(add 1 2)   ;=> 3            <3>
(add 3 4)   ;=> 7            <3>

((fn [a b] (* a b)) 2 3)   ; <4>
;=> 6
....
<1> ``fn``으로 무명의 함수 객체를 생성한다.
<2> 생성된 무명의 함수 객체에 이름을 부여한다.
<3> 이름이 부여된 함수는 같은 이름으로 여러 번 호출될 수 있다.
<4> 여기서 ``fn``으로 생성된 함수는 이름이 없으므로, 다른 곳에서는 호출될 수 없고
    이곳에서만 호출된 후 적절한 시점에 garbage collecting된다.

위의 `fn` 특수 형식의 구문을 설명한 부분을 보면, ``fn``이 무명의 함수 객체를 생성하는 일을
맡고 있음에도 불구하고, 선택적으로 이름을 붙일 수 있게 해 놓은 것을 알 수 있다. 무명
함수가 이름을 가질 수 있다니 대체 이게 무슨 일이란 말인가 하고 생각할 수도 있지만, 그만한
이유는 있다.

첫 번째 이유는, ``fn``으로 정의한 함수가 `fn` 함수 내에서 자기 자신을 호출해야 하는 경우가
종종 있기 때문이다. 이때 이름이 없다면 자기 자신을 다시 호출할 방법이 없게 된다.

[source]
....
((fn fact [x]
   (if (= x 0)
     1
     (* x (fact (dec x)))))
 5)
;=> 120

(fact 5)   ; <1>
;>> CompilerException java.lang.RuntimeException: Unable to resolve symbol: fact in this context,
....
<1> `fn` 안에서 지정한 익명 함수 이름 ``fact``는 이 함수 안에서만 유효하다. 이 함수 밖에서
    호출해서 예외가 발생했다.

두 번째 이유는, 하나의 파일 안에서 ``fn``으로 정의한 무명 함수가 여러 개 있는데, 그 중에
하나에서 에러가 발생하면, 디버깅할 때 어느 무명 함수에서 에러가 발생했는지 구분하기가
힘들어진다. 이런 경우에 매 무명 함수마다 이름을 지정해 놓으면, 에러 메시지에 그 이름을
함께 출력해 주므로 디비깅할 때 도움이 될 수 있기 때문이다. 예를 통해 확인해 보자.

[source]
....
(reduce (fn [num] (inc num)) [1 2 3 4])     ; <1>
(filter (fn [num] (even? num)) [1 2 3 4])
;>> ArityException Wrong number of args (2) passed to: user/eval12399/fn--12400   ; <2>
....
<1> ``reduce``의 첫 번째 인수는 두 개의 인수를 받는 함수이어야 하는데, 한 개를 받는 함수가
    주어져 예외를 일으킨다.

<2> 하지만, 에러 메시지에 나와 있는 함수명(pass:q[`fn--12400`])은 식별하기 힘든 이름을
    반환하고 있어 어느 익무명 함수가 에러를 일으켰는지 구분하기 힘들다.

위와 같은 경우에 각각의 무명 함수에 이름을 부여하면, 다음과 같이 좀 더 식별하기 쉬운
함수명(pass:q[`fn-a--12408`])을 가진 에러 메시지가 출력된다.

[source]
....
(reduce (fn fn-a [num] (inc num)) [1 2 3 4])
(filter (fn fn-b [num] (even? num)) [1 2 3 4])
;>> ArityException Wrong number of args (2) passed to: user/eval12407/fn-a--12408
....


=== 함수 리터럴: #(...)

indexterm:[함수 리터럴: #()]

함수 리터럴 ``#()``을 사용하면, ``fn``을 사용하는 것보다 더 간결한 형태로 무명 함수를
생성할 수 있다. 함수 리터럴 안에서 함수의 인수는 `%1`, `%2`, `%3`, ... 형식으로 표현된다.

함수 인수 ``%1``은 숫자 1을 생략하고 ``%``처럼 사용할 수도 있는데, 일반적으로 다음과 같은
코딩 스타일이 권장된다.

인수가 한 개뿐인 무명 함수는 ``%``를 사용하는 것이 좋다.

[source]
....
;; good
#(Math/round %)

;; bad
#(Math/round %1)
....

인수가 두 개 이상인 무명 함수는 ``%1``을 사용하는 것이 좋다.

[source]
....
;; good
#(Math/pow %1 %2)

;; bad
#(Math/pow % %2)
....

함수 리터럴의 경우에는, ``%&``를 통해서 가변 인수(vararg)를 받는다.

[source]
....
(#(apply max %&) 1 2 3)
;=> 3
....

.``fn``과 함수 리터럴 ``#()``의 차이
[sidebar]
****
``fn``과 함수 리터럴 ``#()``은 둘다 익명 함수를 반환한다는 공통점이 있지만 다음과 같은
차이점도 있다.

첫 번째 차이점은 ``fn``은 암묵적으로 코드 블럭을 감싸는 ``(do ...)``가 들어가 있어서
함수의 본체에 여러 개의 식을 나열할 수 있지만, 함수 리터럴 ``#()``은 그렇지 않아서 여러
개의 식을 나열하려면 명시적으로 ``(do ...)``로 감싸 주어야만 한다.

[source]
....
((fn [x]
   (println x)
   (* x 2))
 5)
;>> 5
;=> 10

(#(do (println %)
      (* % 2))
 5)
;>> 5
;=> 10
....

두 번째 차이점은, ``fn``은 중접해 사용할 수 있지만, 함수 리터럴 ``#()``은 중첩해서 사용할
수 없다.

[source]
....
((fn [a]
  ((fn [b] (* a b))
   (inc a)))
 2)
;=> 6

;; 위의 코드를 함수 리터럴을 사용해서 표현했다.
((#(* % %)   ; <1>
  (inc %))
 2)
;>> CompilerException java.lang.RuntimeException: Unable to resolve symbol: % in this context
....

<1> 첫 번째 `%` 기호는 위의 코드의 ``a``를, 두 번째 `%` 기호는 위의 코드의 ``b``를 염두에
    두고 코드를 작성했지만, 이 `%` 기호가 내부 무명 함수의 인수인지, 외부 무명 함수의
    인수인지를 구별할 방법이 달리 없기 때문에, 함수 리터럴의 중첩은 허용되지 않는다.
****

















== 주요 함수들

아래의 함수들 중 일부는 반환값으로 변환자(transducer)를 반환한다. 변환자에 대해서는
link:../Transducers/transducers.html[변환자] 장에서 자세히 설명할 것이므로, 변환자를
반환하는 예는 생략할 것이다.

=== map/mapv/map-indexed

함수형 언어에서는 다음의 자바 코드에서처럼 index를 증가시키며 반복문을 실행하는 경우가
극히 드물다.

[source,java]
....
import java.util.Arrays;

public class Sample {
    public static void main(String[] args) {
        int[] array1 = {0, 1, 2, 3, 4};
        int[] array2 = array1;

        for (int i = 0; i < array1.length; i++) {   // <1>
            array1[i] += 1;                         // <2>
        }

        System.out.println(Arrays.toString(array1));   // [1, 2, 3, 4, 5]
        System.out.println(Arrays.toString(array2));   // [1, 2, 3, 4, 5]
    }
}
....
<1> 반복을 수행하는 부분이다.
<2> 실제 일을 수행하는 부분이다.

위의 코드가 지니고 있는 문제점을 지적해 보자.

첫 번째 문제점은, 반복문을 처리할 때 '반복을 수행하는 부분'과 '실제 일을 수행하는 부분'을
분리할 수 없어, '반복을 수행하는 부분'을 매번 코딩해 주어야 한다는 것이다. 이 둘을 분리할
수 있다면, '반복을 수행하는 부분'을 매번 코딩할 필요가 없어진다. 컴퓨터가 가장 잘 하는
일이 인간을 대신해 단순한 일을 반복해 처리해 주는 것인데, 정작 그런 프로그램을 코딩하고
있는 프로그래머들은 단순 반복적인 코딩을 당연히 여기고 있다는 사실은 일종의 아이러니가
아닐 수 없다. 클로저에서는 이러한 문제점을 고차 함수(HOF: Higher Order Functions)를 통해
해결한다. 고차 함수는 함수 객체를 인수로 넘기는 함수를 말한다.

두 번째 문제점은, `array1` 변수를 참조하고 있는 `array2` 변수의 값도 함께 변경되었다는
것이다. 이것이 의도한 결과라면 문제가 안되겠지만, 의도하지 않은 것이라면 버그를 초래할
가능성이 높다. 실제로 프로그래밍할 때 주의해서 프로그래밍하지 않으면, 의도하지 않은
레퍼런스의 참조로 인한 버그는 흔한 일이다. 클로저에서는 이런 문제점을 불변값(immutable
value)을 통해 해결한다.

고차 함수 중에서 가장 많이 사용되는 `map` 함수를 통해서 이를 확인해 보자.

==== map
indexterm:[map]

.map 함수
[listing]
----
(map f)       => transducer
(map f coll+) => lazy-seq

f := (fn [element] ...)
----

`map` 함수는 다음과 같이 수학에서 한 집합에서 다른 집합으로 바꾸는 작업을 한다. ``f``는
``coll``의 각 요소에 적용된다.

[listing]
----
   X                 Y
 +----+   (f x1)   +----+
 | x1 |  ------->  | y1 |
 |    |   (f x2)   |    |
 | x2 |  ------->  | y2 |
 |    |   (f x3)   |    |
 | x3 |  ------->  | y3 |
 |    |   (f x4)   |    |
 | x4 |  ------->  | y4 |
 +----+            +----+
----

[CAUTION]
====
`map` 함수는 맵 자료형을 만드는 함수가 아닌 것에 주의한다. 요소들을 인수로 받아 맵
자료형을 만드는 함수는 ``hash-map``이다.

[source]
....
(hash-map :a 1 :b 2)   ;=> {:b 2, :a 1}
....

참고로, `set` 함수는 집합이 아닌 컬렉션 자료형을 집합 자료형으로 변환하는 역할을
수행한다. 요소들을 인수로 받아 집합 자료형을 만드는 함수는 ``hash-set``이다.

[source]
....
;; 벡터를 집합으로 변환한다.
(set [1 1 2 2 3 3 4 5])   ;=> #{1 4 3 2 5}

;; 집합의 요소들을 받아 집합을 생성한다.
(hash-set 1 1 2 2 3 3 4 5)
#{1 4 3 2 5}
....
====

`map` 함수 자체에 '반복을 수행하는 부분'이 이미 내장되어 있다. 그래서 아래의 코드에서 함수
인수로 들어가는 ``inc``가 '실제 일을 수행하는 부분'만을 담당하게 된다. 고차 함수 자체가
'반복을 수행하는 부분'을 제공하고, 이 함수의 인수로 들어가는 함수 ``inc``와 데이터 ``a``가
'실제 수행해야 할 일'을 지정하고 있는 것이다. 고차 함수의 도입으로 인해, index를 순회하며
'반복을 수행하는 부분'를 더 이상 매번 작성할 필요가 없어진 것이다! 별 것 아닌 것 같지만,
코드에서 가능한 한 중복을 제거하는 것 자체가 프로그래밍에 상당히 중요한 요소임을
간과해서는 안된다. 한 두번 반복하는 것이야 문제될 것이 없겠지만, 프로그래머 일생에 결쳐
계속해서 반복해야 한다면 이것은 큰 문제가 아닐 수 없다. 왜 같은 일을 굳이 반복해야 하는가?
그렇게 하지 않을 수 있는 방법이 있는데도 말이다.

그리고 `map` 함수에서 ``a``의 값을 사용했지만, ``a``의 값은 변경되지 않았다. 따라서
``a``를 참조하고 있는 ``b``의 내용도 당연히 변하지 않았다. 클로저에서는 모든 값이
immutable value이다. 그래서 ``(map inc a)``를 평가해 나온 값 ``(1 2 3 4 5)``는 ``a``의 값
``[0 1 2 3 4]``를 변경한 것이 아니라, 새로운 값을 생성한 것이다.

[source]
....
(def a [0 1 2 3 4])
(def b a)

(map inc a)   ;=> (1 2 3 4 5)

a             ;=> [0 1 2 3 4]
b             ;=> [0 1 2 3 4]
....

`map` 함수는 다음과 같이 `coll` 인수를 두 개 이상 받을 수도 있다. 이 경우에는 맨 먼저 각
컬렉션의 첫 번째 요소들을 모두 가져와 함수 ``+``에 적용하고, 그 다음에는 각 컬렉션의 두
번째 요소들을 모두 가져와 함수에 적용하는 방식으로 계속 이어진다.

[source]
....
(map + [1 2 3] [10 20 30] [100 200 300])
;=> (111 222 333)

;; 즉, 다음을 실행한 결과와 같다.
(list (+ 1 10 100) (+ 2 20 200) (+ 3 30 300))
;=> (111 222 333)
....

제공되는 컬렉션의 요소 수가 다르면, 요소의 수가 가장 적은 컬렉션에 맞추어 반환한다.

[source]
....
(map + [1 2] [10 20 30] [100 200 300])
;=> (111 222)
....

다음과 같은 코드는 예외가 발생하는데, `inc` 함수는 인수를 한 개만 받는 함수이기
때문이다. 즉, `map` 함수에 제공되는 컬렉션의 개수가 세 개이면, 세 개의 인수를 받을 수 있는
함수가 `map` 함수의 첫 번째 인수 자리에 와야 한다.

[source]
....
(map inc [1 2 3] [10 20 30] [100 200 300])
;>> ArityException Wrong number of args (3) passed to: core/inc

 ;; 즉, 다음을 실행한 결과와 같기 때문에 예외가 발생한다.
(list (inc 1 10 100) (inc 2 20 200) (inc 3 30 300))
;>> ArityException: Wrong number of args (3) passed to: core/inc
....

마지막으로 초보자들이 `map` 함수를 사용할 때 종종 겪는 실수를 한 가지 언급하겠다. 그것은
바로 ``map``의 첫 번째 함수 인수 자리에 부수 효과(side effects)를 일으키는 함수를 사용하는
데서 비롯되는 실수이다. 이것은 lazy-seq를 반환하는 모든 클로저 함수에 해당하는
이야기이기도 하다.

다음의 예제를 실행하면, 기대와는 달리 숫자 ``1 2 3``이 화면에 출력되지 않는다. 그 이유는
`map` 함수가 반환한 lazy-seq가 실현(realized)될 기회를 갖지 못하기 때문이다. lazy-seq는
실현될 기회를 갖지 못하면 영원히 실현되지 않는다.

[source]
....
(let [num [1 2 3]]
  ;; map 함수의 실행 결과로 lazy-seq가 반환되지만, 쓰여지지 않은채 그냥 버려진다.
  ;; 즉, 실현될 기회를 갖지 못하게 된다. 따라서 println 함수가 호출될 기회기 없다.
  (map println num)
  (conj num 4))
;=> [1 2 3 4]
....

반면에 아래의 코드에서는, `map` 함수가 마찬가지로 laz-seq를 반환하지만, 또한 ``let``의
반환값으로도 반환되고 있다. 이후 이 반환값을 REPL(Read-Eval-Print-Loop)에서 출력하는
과정에서 실현될 기회를 갖게 되어, 숫자 ``1 2 3``이 화면에 출력된다.

[source]
....
(let [num [1 2 3]]
  (map println num))
;>> 1
;>> 2
;>> 3
;=> (nil nil nil)
....

indexterm:[doseq]

따라서 부수 효과가 주목적이라면, `map` 함수가 아니라 부수 효과를 위해 준비된 ``doseq``을
사용해야 한다. 아울러 아래의 코드에서 ``doseq``이 ``(2 4 6)``의 결과를 반환할 것이라
예상하겠지만,``doseq``은 마지막 반환 결과를 무시하고, 무조건 ``nil``을 반환한다는 점에
주목하자. 한 마디로 ``doseq``은 오로지 부수 효과만을 수행할 목적으로 마련된 매크로라고
보면 된다.

[source]
....
(doseq [num [1 2 3]]
  (println num)
  (* num 2))
;>> 1
;>> 2
;>> 3
;=> nil
....

indexterm:[dorun]

부득이하게 `map` 함수를 부수 효과를 적용하는데 사용해야만 하는 상황이라면, lazy-seq를 강제로
실현해 주는 ``dorun``을 이용하는 방법도 있다.

[source]
....
(let [num [1 2 3]]
  (dorun (map println num))
  (conj num 4))
;>> 1
;>> 2
;>> 3
;=> [1 2 3 4]
....

indexterm:[run!]

그리고 클로저 1.7.0에서 새로 도입된 `run!` 함수를 사용할 수도 있다.

[source]
....
(let [num [1 2 3]]
  (run! println num)
  (conj num 4))
;>> 1
;>> 2
;>> 3
;=> [1 2 3 4]
....


==== mapv

indexterm:[mapv]

.mapv 함수
[listing]
----
(mapv f coll+) => vector
----

`mapv` 함수는 lazy-seq를 반환하는 `map` 함수와는 달리 벡터 자료형을 반환한다. 클로저에서
지연 평가(lazy evaluation)가 일어나는 유일한 자료형은 lazy-seq이다. 다시 말해, lazy-seq
자료형을 제외한 모든 자료형은 즉시 평가(eager evaluation)가 일어난다. 따라서 `mapv` 함수는
벡터 자료형을 반환하므로 지연 평가가 일어나지 않고 즉시 평가(eager evaluation)가 일어나게
되어, 함수 인수 `f` 자리에 부수 효과가 일어나는 함수가 올 수 있다.

[source]
....
(let [num [1 2 3]]
  (mapv println num)
  (conj num 4))
;>> 1
;>> 2
;>> 3
;=> [1 2 3 4]
....

==== map-indexed

indexterm:[map-indexed]

.map-indexed 함수
[listing]
----
(map-indexed f)       => transducer
(map-indexed f coll+) => lazy-seq

f := (fn [index element] ...)
----

``map-indexed``는 함수 인수인 ``f``가 `[index item]` 함수의 두 개의 인수를 받게 된다는
점을 제외하고는 `map` 함수와 동일하다. 이 함수는 현재 처리하고 있는 ``coll``의 요소가
``coll``의 몇 번째 항목인지 알고 싶을 때 사용하면 유용하다.

[source]
....
(map-indexed (fn [index item] [index item]) "foobar")
;=> ([0 \f] [1 \o] [2 \o] [3 \b] [4 \a] [5 \r])
....


=== filter/filterv/remove

==== filter

indexterm:[filter]

.filter 함수
[listing]
----
(filter pred)       => transducer
(filter pred coll?) => lazy-seq
----

`filter` 함수는 ``coll``의 요소 ``element``에 대해 ``(pred element)``가 논리적 참인
``element``들로 이루어진 lazy-seq를 리턴한다.

[listing]
----
   X                               Y
 +----+    (pred x1) => true     +----+
 | x1 | -----------------------> | x1 |
 |    |    (pred x2) => false    |    |
 | x2 | -----------------------> |    |
 |    |    (pred x3) => true     |    |
 | x3 | -----------------------> | x3 |
 |    |    (pred x4) => false    |    |
 | x4 | -----------------------> |    |
 +----+                          +----+
----

[source]
....
(filter even? (range 10))
;=> (0 2 4 6 8)

(filter identity [false nil 10 :a "hello" [1 2 3]])
;=> (10 :a "hello" [1 2 3])
....


==== filterv

indexterm:[filterv]

[listing]
----
(filterv pred coll) => vector
----

`filterv` 함수는 ``map``과 `mapv` 함수의 관계와 같다. 즉, lazy-seq를 반환하는 `filter`
함수와는 달리, 벡터를 반환한다.

[source]
....
(filterv even? (range 10))
;=> [0 2 4 6 8]

(filterv identity [false nil 10 :a "hello" [1 2 3]])
;=> [10 :a "hello" [1 2 3]]
....


==== remove

indexterm:[remove]

[listing]
----
(remove pred)       => transducer
(remove pred coll) => lazy-seq
----

``remove``는 ``filter``와 정반대이다. 즉, ``coll``의 요소 ``element``에 대해 ``(pred
element)``가 논리적 참인 ``element``를 모두 제거한 요소들로 이루어진 lazy-seq를 반환한다.

[source]
....
(remove even? (range 10))
;=> (1 3 5 7 9)

(remove identity [false nil 10 :a "hello" [1 2 3]])
;=> (false nil)
....


=== reduce/reduce-kv/reductions

indexterm:[reduce]

==== reduce

.reduce 함수
[listing]
----
(reduce f init? coll)

f := (fn [acc element] ...)
     이 함수의 첫 번쨰 인수인 acc에 누적(accumulation)된 값이 담기고,
     두 번째 인수 element에 coll의 각 요소가 담긴다.
init := 초기값
----

`reduce` 함수는 약간 복잡하기는 하지만, 잘만 사용하면 왠만한 일은 거의 처리할 수 있을
정로로 매우 강력한 함수이므로, 잘 이해해 두도록 한다.

먼저 그림을 통해 이해해 보자. 만약 이 함수가 ``(reduce f init [x1 x2 x3 ... xn])``과 같이
호출되었다고 가정하면, 계산은 다음과 같이 진행된다.

[listing]
----
(f init x1)    => acc1  ; <1>
    |--------------|
    v
(f acc1 x2)    => acc2  ; <2>
    |--------------|
    v
(f acc2 x3)    => acc3
    |--------------|
    v
(f accn xn)    => acc (최종값)   ; <3>
----
<1> 초기값 ``init``과 첫 번째 요소 ``x1``을 함수 ``f``에 적용한다.
<2> 위에서 얻은 결과값 ``acc1``과, 두 번째 요소 ``x2``를 함수 ``f``에 다시 적용한다.
<3> 이런 식으로 계산된 최종값 ``acc``가 ``reduce`` 함수의 반환값으로 리턴된다.

실제 예를 통해 구체적으로 살펴 보면, ``(reduce + 10 [1 2 3 4 5])``가 계산되는 과정은
다음과 같다.

[listing]
----
(+ 10 1)   => 11
   |----------|
   v
(+ 11 2)   => 13
   |----------|
   v
(+ 13 3)   => 16
   |----------|
   v
(+ 16 4)   => 20
   |----------|
   v
(+ 20 5)   => 25 (최종값)
----

만약 ``(reduce f [x1 x2 x3 ... xn])``에서처럼 ``init``이 주어지지 않으면, 계산은 다음과
같이 진행된다.

[listing]
----
(f x1 x2)       => acc1
    |---------------|
    v
(f acc1 x3)     => acc2
    |---------------|
    v
(f acc2 x4)     => acc3
    |---------------|
    v
(f accn xn)     => acc (최종값)
----

다시 실제 예를 통해 구체적으로 살펴 보면, ``(reduce + [1 2 3 4 5])``가 계산되는 과정은
다음과 같다.

[listing]
----
(+ 1 2)    => 3
   |----------|
   v
(+ 3 3)    => 6
   |----------|
   v
(+ 6 4)    => 10
   |----------|
   v
(+ 10 5)   => 15 (최종값)
----

[CAUTION]
====
`reduce` 함수를 사용할 때 한 가지 주의해야 할 점은, 이 함수는 lazy-seq를 반환하지 않는다는
것이다. 따라서 메모리 사용량이 아주 많은 연산을 수행할 떄는 다음처럼 문제가 발생할 수
있다.

[source]
....
(reduce + (range))
;>> ArithmeticException integer overflow

(reduce conj [] (range))
;>> Exception in thread "main" java.lang.OutOfMemoryError: Java heap space
....
====

==== reduce-kv

indexterm:[reduce-kv]

.reduce-kv 함수
[listing]
----
(reduce-kv f init coll)

f := (fn [acc key value] ...)
     이 함수의 첫 번쨰 인수인 acc에 누적(accumulation)된 값이 담기고, 두 번째 인수에
     coll의 각 요소의 key가, 세 번쨰 인수에 각 요소의 value가 담긴다.
init := 초기값
----

`reduce-kv` 함수는 ``reduce``와 유사하지만, 함수 인수 ``f``가 받아들일 인수가 ``[acc
element]``의 2개가 아니라 ``[acc key value]``의 3개이다. 그리고 ``init`` 값이 반드시
제공되어야 하고, `coll` 자리에는 맵이나 벡터가 와야 한다.

[source]
....
(reduce-kv (fn [acc k v]
             (assoc acc k (inc v)))
           {}
           {:a 1 :b 2 :c 3 :d 4})
;=> {:a 2, :b 3, :c 4, :d 5}

;; 벡터도 key가 벡터의 인덱스이고, value가 벡터의 각 요소인 맵이다.
(reduce-kv (fn [acc k v]
             (assoc acc k (inc v)))
           {}
           [10 20 30])
;=> {0 11, 1 21, 2 31}
....


==== reductions

indexterm:[reductions]

.reductions 함수
[listing]
----
(reductions f init? coll) => lazy-seq
----

이 함수는 `reduce` 함수의 '초기값'과 '매 단계별 누적값들'을 모두 담은 lazy-seq를
반환한다. `reduce` 함수의 누적값의 진행 상황을 알아보고 싶을 때 유용하다.

[source]
....
(reductions + [1 2 3 4])
;=> (1 3 6 10)

(reductions + 10 [1 2 3 4])
;=> (10 11 13 16 20)

(reductions conj [] [1 2 3 4])
;=> ([] [1] [1 2] [1 2 3] [1 2 3 4])

(reductions conj () [1 2 3 4])
;=> (() (1) (2 1) (3 2 1) (4 3 2 1))
....


=== apply

indexterm:[apply]

.apply 함수
[listing]
----
(apply f element* coll)
----

`apply` 함수는 ``element``가 제공되지 않을 경우에는 ``coll``을 함수 ``f``에 단순히
적용(apply)한다. `element`들이 제공될 경우에는 ``element``와 ``coll``을 리스트로 한데 묶은
후, 함수 ``f``를 적용한다.

[source]
....
(apply min [10 20 30])
;=> 10

;; 위는 아래를 실행한 것과 같다.
(min 10 20 30)
;=> 10

(apply min 0 1 2 [10 20 30])
;=> 0

;; 위는 아래를 실행한 것과 같다.
(min 0 1 2 10 20 30)
;=> 0

;; min 함수는 인수가 전혀 없으면 예외가 발생한다.
(min)
;>> ArityException Wrong number of args (0) passed to: core/min

;; 이번에는 디폴트 값으로 0을 제공하고 있어서 예외가 발생하지 않는다.
(apply min 0 [])
;=> 0
....

.apply와 reduce 함수의 차이
[sidebar]
****
프로그램을 짜다 보면, ``apply``와 ``reduce`` 함수를 언제 사용해야 할지 잘 판단이 서지 않는
경우가 종종 있다. 그래서 여기서는 그 차이점을 알아보도록 하겠다.

예를 들어, 아래의 코드를 보면, ``apply``와 ``reduce`` 함수가 동일한 결과값을 반환하고
있다. 계산 결과는 같게 나오지만, 계산 과정은 같지 않다.

[source]
....
(apply + [1 2 3 4 5])    ; => 15
(reduce + [1 2 3 4 5])   ; => 15
....

그렇다면 이 두 함수의 차이는 무엇인가? 그 차이를 알아보기 위해 먼저 ``add``라는 함수를
다음과 같이 정의하고 테스트를 진행해 보자.

[source]
....
(defn add [a b]
  (+ a b))

(apply add [1 2 3 4 5])
;>> clojure.lang.ArityException: Wrong number of args

(reduce add [1 2 3 4 5])
; => 15
....

``apply`` 함수의 경우에만 ArrityException이 발생했다. 그 이유는 `add` 함수가 정확히 2개의
인수만을 받아들이도록 정의되어 있기 때문이다. 다시 말해, ``(apply add [1 2 3 4 5])``로
호출하면, `(add 1 2 3 4 5)` 식으로 실행되므로 당연히 에러가 발생할 수밖에 없다.

이에 반해, `+` 함수는 인수가 `[a b & more]` 식으로 정의되어 있다. 다시 말해, `+` 함수는
원래 다인수 함수로 정의되어 있어서, `(apply + [1 2 3 4 5])` 식으로 호출하면 `(+ 1 2 3 4
5)` 식으로 실행되므로, 결과적으로 에러가 발생하지 않는다.

반면에, ``(reduce add [1 2 3 4 5])``는 `(add (add (add (add 1 2) 3) 4) 5)` 식으로
실행되므로, 에러가 발생하지 않는 것이다.

따라서 '3개 이상의 인수를 받아들이는' 함수를 호출 하는 경우에는 `apply` 함수를 사용할 수
있지만, '2개의 인수만을 받아들이는' 함수를 호출하는 경우에는 `reduce` 함수를 사용해야
한다.

조금 설명을 보충하자면, ``apply``는 두 번째 인수로 나온 컬렉션의 괄호를 풀어 헤쳐, 첫 번째
인수로 주어진 함수에 일괄적으로 적용하는 함수로 이해하면 좋다. 이에 반해 ``reduce``는
일종의 재귀 함수로 이해하면 된다. 여기서 `+` 함수의 첫 번째 인수는 누적값을 계속 쌓아
나가는 용도로, 두 번째 인수는 새로운 요소의 값을 받아들이는 용도로 사용된다.
****

=== keep/keep-indexed

==== keep

indexterm:[keep]

.keep 함수
[listing]
----
(keep f)      => transdducer
(keep f coll) => lazy-seq

f := (fn [element] ...)
----

`keep` 함수는 ``coll``의 각 ``element``에 대해 ``(f element)``을 수행한 후, 그 반환값이
``nil``이 아닌 값들만을 담은 lazy-seq를 반환한다.

예를 들어, 컬렉션 ``[0 1 2 3 4 5]``의 값들 중에서 홀수들만을 취해 그 수를 제곱하고 싶을 떄
는 ``keep`` 함수를 이용해 다음과 같이 하면 된다.

[source]
....
(keep (fn [num]
        (when (odd? num) (* num num)))
      [0 1 2 3 4 5])
;=> (1 9 25)
....

이런 경우 ``map`` 함수를 이용하면 다음과 같은 원하지 않는 결과도 반환하게 된다.

[source]
....
(map (fn [num]
        (when (odd? num) (* num num)))
     [0 1 2 3 4 5])
;=> (nil 1 nil 9 nil 25)
....

결론적으로 말하자면, `map` 함수는 첫 번째 인수인 함수 ``f``가 어떤 값을 반환하느냐에 상관
없이 무조건 입력값에 해당하는 개수만큼의 결과값을 반환하지만, `keep` 함수는 입력값들
중에서 처리를 원하지 않는 값들의 경우에는 일부러 nil을 반환하는 것을 통해, 처리를 원치
않는 값들을 결과값에 포함되지 않도록 할 수 있다.


==== keep-indexed

indexterm:[keep-indexed]

.keep-indexed 함수
[listing]
----
(keep-indexed f)      => transdducer
(keep-indexed f coll) => lazy-seq

f := (fn [index element] ...)
----

``keep-indexed``는 함수 인수 ``f``가 ``coll``의 ``index``와 ``element`` 두 개의 인수를
받는다는 점을 제외하고는 `keep` 함수와 동일하다.

[source]
....
(keep-indexed (fn [index num]
                (if (pos? num) index))
              [-9 0 29 -7 45 3 -8])
;=> (2 4 5)
....


// for doseq dotimes concat mapcat
// partion/partition-all/partition-by group-by split/split-at/split-with distinct
// some not-any? every? not-every?
// complement partial comp
// rand/rand-int/rand-nth/shuffle
// 재귀: loop recur trampoline











